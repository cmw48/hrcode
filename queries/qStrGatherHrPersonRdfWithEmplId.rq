#
# qStrGatherHrPersonRdfWithEmplId.rq
# with an emplID, 
# construct all HR rdf about a person
# updated 140720
# added hr preferred and legal name parts
# attach to VIVO vcard

PREFIX hr: <http://vivo.cornell.edu/ns/hr/0.9/hr.owl#>  
PREFIX cuvivo: <http://vivo.cornell.edu/individual/> 
PREFIX foaf: <http://xmlns.com/foaf/0.1/> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX vivo: <http://vivoweb.org/ontology/core#> 
PREFIX newhr: <http://vivoweb.org/ontology/newhr#>
PREFIX vivo: <http://vivo.library.cornell.edu/ns/0.1#>
PREFIX titlemap: <http://vivo.library.cornell.edu/ns/hr/titleMapping#> 
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX obo: <http://purl.obolibrary.org/obo/>

CONSTRUCT { 
    ?vivoperson hr:emplId ?variableEmplId .
    ?vivoperson rdfs:label ?hrislabel .
    ?vivoperson hr:netId ?hrisnetId .
    ?vivoperson obo:ARG_2000028 ?vivovcard . 
    ?vivovcard obo:ARG_2000029 ?vivoperson . 
    ?vivovcard vcard:hasName ?vivoNameUri .
    ?vivoNameUri vcard:familyName ?hrislastName . 
    ?vivoNameUri vcard:givenName ?hrisfirstName . 
    ?vivoNameUri hr:legalNameString ?hrisLegalNameString .
    ?vivoNameUri hr:preferredFirstName ?hrisPreferredFirstName . 
    ?vivoNameUri hr:preferredLastName ?hrisPreferredLastName . 
    ?vivoNameUri  hr:preferredMiddleInitial ?hrisPreferredMiddleInitial . 
    ?vivoNameUri hr:preferredMiddleName ?hrisPreferredMiddleName . 
    ?vivoNameUri hr:preferredNameString ?hrisPreferredNameString . 
    ?vivoNameUri hr:preferredSocialSuffix ?hrisPreferredSocialSuffix. 
    ?vivovcard vcard:hasEmail ?vivoEmailUri .
    ?vivoEmailUri rdf:type vcard:Email .
    ?vivoEmailUri vcard:email ?hrisemail .
    ?vivoperson hr:WorkingTitle ?hriswtitle .
  }   
WHERE {
  BIND (str("VARVALUE") as ?variableEmplId)
  # querying HRIS endpoint 
  SERVICE <HRISSERV>
    {
    ?hrisperson hr:emplId ?variableEmplId .
    ?hrisperson rdfs:label ?hrislabel .
    ?hrisperson hr:netId ?hrisnetId .
    OPTIONAL { ?hrisperson hr:legalNameString ?hrisLegalNameString . }
    OPTIONAL { ?hrisperson hr:preferredFirstName ?hrisPreferredFirstName . }
    OPTIONAL { ?hrisperson hr:preferredLastName ?hrisPreferredLastName . }
    OPTIONAL { ?hrisperson hr:preferredMiddleInitial ?hrisPreferredMiddleInitial . }
    OPTIONAL { ?hrisperson hr:preferredMiddleName ?hrisPreferredMiddleName . }
    OPTIONAL { ?hrisperson hr:preferredNameString ?hrisPreferredNameString . }
    OPTIONAL { ?hrisperson hr:preferredSocialSuffix ?hrisPreferredSocialSuffix. }
    #OPTIONAL { ?hrisperson hr:primaryUnitCode ?hrisPrimaryUnitCode . }
    OPTIONAL { ?hrisperson vivo:middleInitial ?hrisMiddleInitial . }
    OPTIONAL { ?hrisperson hr:WorkingTitle ?hriswtitle .}
    OPTIONAL { ?hrisperson vivo:email ?hrisemail .}
    OPTIONAL { ?hrisperson foaf:firstName ?hrisfirstName .}
    OPTIONAL { ?hrisperson foaf:lastName ?hrislastName .}
    OPTIONAL { ?hrisperson hr:PrefName ?hrisPrefName .}
   }
   
   SERVICE <VIVOSERV>
    {
     #?vivoperson rdf:type foaf:Person .
     ?vivoperson hr:emplId ?variableEmplId .
     OPTIONAL { ?vivoperson obo:ARG_2000028 ?vivovcard . }
   
     OPTIONAL { ?vivovcard vcard:hasName ?vivoNameUri .
     #   OPTIONAL { ?vivoNameUri vcard:givenName ?givenName . }
     #   OPTIONAL { ?vivoNameUri vcard:familyName ?familyName . }
     }
     OPTIONAL { ?vivovcard vcard:hasEmail ?vivoEmailUri .
     #    OPTIONAL { ?emailUri vcard:email ?emailString .}
     }
     #OPTIONAL { ?vivovcard vcard:hasTitle ?titleUri . 
     #    OPTIONAL { ?titleUri vcard:title ?titleString . }
     #}   
    
    } 
}
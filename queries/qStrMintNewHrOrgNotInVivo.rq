#
# qStrMintNewHrOrgNotInVivo.rq
# updated 120907
# to be used as a query against a model
# grab the URI from every vivo:Position in the model
# get the orgURI from the D2R position
# return the deptId from the D2R org (and label and UnitIdDesc if available)
# check the orgURI against the VIVO Joseki, query for optional ?vivoorg URI
# return results ONLY where this orgURI does NOT have a pre-existing VIVO org
# construct all org RDF with v1.6 format RDF and HRIS ?orgURI for minting in VIVO
# edited for vivo1.6

PREFIX hr: <http://vivo.cornell.edu/ns/hr/0.9/hr.owl#>  
PREFIX cuvivo: <http://vivo.cornell.edu/individual/> 
PREFIX foaf: <http://xmlns.com/foaf/0.1/> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX vivo: <http://vivoweb.org/ontology/core#> 
PREFIX newhr: <http://vivoweb.org/ontology/newhr#>
PREFIX vivoc: <http://vivo.library.cornell.edu/ns/0.1#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>

CONSTRUCT { 
    ?organization rdf:type foaf:Organization .
    ?organization vivo:relatedBy ?varHrJob .
    ?varHrJob vivo:relates ?organization .
    ?organization rdfs:label ?label .
   
    # mint vcard rdf for non-vivo orgs
    ?organization obo:ARG_2000028 ?hrOrgVcardUri .
    ?hrOrgVcardUri vcard:hasName ?hrOrgVcardNameUri .
    ?hrOrgVcardNameUri obo:ARG_2000029 ?organization .
    ?organization rdfs:label ?label .
 
    # add rdf for workday data properties
    ?organization hr:deptCode ?deptCode .
    ?organization hr:UnitCode ?unitCode .
    ?organization hr:kfsOrgCode ?kfsOrgCode .
    ?organization hr:DeptID ?deptId .
    ?organization vivoc:departmentHRcode ?deptId .
  } 

WHERE {
   BIND (IRI ("VARVALUE") AS ?varHrJob )

   #querying HRIS endpoint	   
   SERVICE <http://vivo-migrate.library.cornell.edu/d2r>
    { 
      ?varHrJob hr:jobInPosition ?position .
      ?position vivo:positionInOrganization ?organization .
      ?organization rdfs:label ?label .
      OPTIONAL {?organization hr:DeptID ?deptId .} 
      OPTIONAL {?organization hr:deptCode ?deptCode . }
      OPTIONAL {?organization hr:UnitCode ?unitCode . }
      OPTIONAL {?organization hr:kfsOrgCode ?kfsOrgCode . }
    }
    # querying VIVO endpoint
    SERVICE <http://vivo-migrate.library.cornell.edu/vivo16/sparql>
      {
        OPTIONAL {?vivoorg vivoc:departmentHRcode ?deptCode . }
      }  
    # There is no matching VIVO org. Mint new org with HR uri.
    FILTER (!BOUND(?vivoorg)) 
      BIND (IRI (CONCAT(?organization, "-vc")) AS ?hrOrgVcardUri )  .
      BIND (IRI (CONCAT(?organization, "-vc-name")) AS ?hrOrgVcardNameUri ) . 
}
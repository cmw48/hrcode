# qStrOnePersonVIVORDF
# updated 140623 - rewritten for vivo1.6
# with person URI, 
# return all VIVO RDF about that person
# TODO: look closely at titles. 
# TODO: Are there any other legacy data properties to look at?

PREFIX hr: <http://vivo.cornell.edu/ns/hr/0.9/hr.owl#>  
PREFIX cuvivo: <http://vivo.cornell.edu/individual/> 
PREFIX foaf: <http://xmlns.com/foaf/0.1/> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX vivo: <http://vivoweb.org/ontology/core#> 
PREFIX newhr: <http://vivoweb.org/ontology/newhr#>
PREFIX vivoc: <http://vivo.library.cornell.edu/ns/0.1#>
PREFIX titlemap: <http://vivo.library.cornell.edu/ns/hr/titleMapping#> 
PREFIX acti: <http://vivoweb.org/ontology/activity-insight#>
PREFIX vitro: <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX obo: <http://purl.obolibrary.org/obo/>

CONSTRUCT { 
  <VARVALUE> hr:emplId ?vivoemplId .
  <VARVALUE> rdfs:label ?vivolabel .
  <VARVALUE> hr:netId ?vivonetId .
  <VARVALUE> hr:emplId ?vivoemplId .
  <VARVALUE> hr:WorkingTitle ?vivowtitle .
  <VARVALUE> vivo:email ?vivoemail .
  <VARVALUE> obo:ARG_2000028 ?vcard . 
  
  ?vcard obo:ARG_2000029 <VARVALUE> . 
  ?vcard vcard:hasName ?nameUri .
    ?nameUri a vcard:Name . 
    ?nameUri vcard:familyName ?familyName . 
    ?nameUri vcard:givenName ?givenName . 
  ?vcard vcard:hasEmail ?emailUri .
    ?emailUri rdf:type vcard:email .
    ?emailUri vcard:email ?emailString .
  ?vcard vcard:hasTitle ?titleUri . 
    ?titleUri rdf:type vcard:Title .
    ?titleUri vcard:title ?titleString . 

  <VARVALUE> hr:primaryWorkingTitle ?primaryTitle .
  <VARVALUE> hr:primaryUnitCode ?primaryUnitCode .
  }   
  
WHERE {
  # querying VIVO endpoint
  SERVICE <VIVOSERV>
    {
     <VARVALUE> rdf:type foaf:Person .
     OPTIONAL { <VARVALUE> obo:ARG_2000028 ?vcard . }

     OPTIONAL { ?vcard vcard:hasName ?nameUri .
        OPTIONAL { ?nameUri vcard:givenName ?givenName . }
        OPTIONAL { ?nameUri vcard:familyName ?familyName . }
     }
     OPTIONAL { ?vcard vcard:hasEmail ?emailUri .
         OPTIONAL { ?emailUri vcard:email ?emailString .}
     }
     OPTIONAL { ?vcard vcard:hasTitle ?titleUri . 
         OPTIONAL { ?titleUri vcard:title ?titleString . }
     }

     OPTIONAL { <VARVALUE>  rdfs:label ?vivolabel .}
     OPTIONAL { <VARVALUE> hr:netId ?vivonetId .}
     OPTIONAL { <VARVALUE> hr:emplId ?vivoemplId .}	
     OPTIONAL { <VARVALUE> hr:WorkingTitle ?vivowtitle .}
     OPTIONAL { <VARVALUE> hr:primaryWorkingTitle ?primaryTitle .}
     OPTIONAL { <VARVALUE> vivo:email ?vivoemail .}
     OPTIONAL { <VARVALUE> foaf:firstName ?vivofirstName .}
     OPTIONAL { <VARVALUE> foaf:lastName ?vivolastName .}
     OPTIONAL { <VARVALUE> hr:primaryUnitCode ?primaryUnitCode .}
    } 
}